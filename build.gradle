allprojects {

    apply plugin: 'idea'
    apply plugin: 'java'

    repositories {
        mavenLocal()
        mavenCentral()
    }
    
    dependencies {
        compile group:'org.junit.jupiter', name:'junit-jupiter-api', version:'5.4.2'
        compile group:'org.junit.jupiter', name:'junit-jupiter-params', version:'5.4.2'

        //junit 5
        testImplementation "org.junit.platform:junit-platform-launcher:1.4.2"
        testImplementation('org.junit.jupiter:junit-jupiter-api:5.4.2') //!IMPORTANT:do not modify, this is the only syntax travis understands.
        testRuntime "org.junit.jupiter:junit-jupiter-engine:5.4.2"
    }
    
    test{
        useJUnitPlatform {}

        testLogging {
            showStandardStreams = true
            events "passed", "skipped", "failed"
        }
        //to get "Test summary: 1 tests, 1 succeeded, 0 failed, 0 skipped"
        afterSuite { desc, result ->
            if (!desc.parent) {
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                logger.error('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            groupId 'org.lint.azzert'
            artifactId 'java-lint-assert'
            version '0.1.0-SNAPSHOT'

            from components.java

            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // eliminate test-scoped dependencies (no need in maven central POMs)
        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        // add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = groovy.lang.Closure.DELEGATE_FIRST

            description 'Serialize and Deserialize Json-LD into Java entities'
            name 'Schema.org Java'
            url 'https://github.com/mautini/schemaorg-java'
            organization {
                name 'com.github.mautini'
                url 'https://github.com/mautini'
            }
            issueManagement {
                system 'GitHub'
                url 'https://github.com/mautini/schemaorg-java/issues'
            }
            licenses {
                license {
                    name 'Apache License 2.0'
                    url 'https://github.com/mautini/schemaorg-java/blob/master/LICENSE'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://github.com/mautini/schemaorg-java'
                connection 'scm:git:git://github.com/mautini/schemaorg-java.git'
                developerConnection 'scm:git:ssh://git@github.com:mautini/schemaorg-java.git'
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenJavaPublication {
        destination = file("$buildDir/generated-pom.xml")
    }
}